# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix build on ARM

pkgname=pyzy
pkgver=1.0
_commit="d7747466562cb8b4bc2934708e29b7643c7bedbc"
pkgrel=4
pkgdesc='The Chinese PinYin and Bopomofo conversion library'
arch=('x86_64')
url='https://github.com/pyzy/pyzy'
license=('LGPL')
depends=('glib2' 'sqlite' 'util-linux')
makedepends=('git' 'doxygen' 'gnome-common' 'python2')
source=("git://github.com/pyzy/pyzy.git#commit=$_commit"
        'pyzy-0.1.0-fixes-compile.patch')
sha512sums=('SKIP'
            '973f1d9928801e028fe8a212403ec421a0fcbb9874745dd6722b1a769d8c9717d5ae88fb62d3fea6d0fc60c6889f3fa9cf17600d5eafcad8fb6b9f28e2677098')

prepare() {
  cd $pkgname
  patch -p1 -i ../pyzy-0.1.0-fixes-compile.patch
}

build() {
  cd $pkgname

  # replace python with python2; see https://github.com/hsumita/libpyzy/issues/1
  find . -name '*.py' -exec sed -ri '1s#(bin/|env )python#\1python2#' '{}' \;

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install
}
