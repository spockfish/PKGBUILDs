# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepends on python-pypandoc, python2-pypandoc

pkgbase=python-voluptuous
pkgname=(python-voluptuous python2-voluptuous)
pkgver=0.11.5
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/v/voluptuous/voluptuous-$pkgver.tar.gz")
sha512sums=('8893434e8e73ebf3682da1970d1d720444de452937ac9123fab53413c88a21d04ad3eb490902d829aa60be6804ff68b8bcf57a1c45807751c3d50be63e9b2551')

prepare() {
  # https://github.com/alecthomas/voluptuous/issues/333
  rm -r voluptuous-$pkgver/*.egg-info

  # https://github.com/alecthomas/voluptuous/pull/332
  sed -e "s/'w'/'wb'/" -e "s/.encode('utf-8')//" -e "s/f.write(long_description)/f.write(long_description.encode('utf-8'))/" -i voluptuous-$pkgver/setup.py

  # Python 3.7
  sed -i '/raise StopIteration/d' voluptuous-$pkgver/voluptuous/schema_builder.py

  cp -a voluptuous-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py build

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py nosetests

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py nosetests
}

package_python-voluptuous() {
  depends=('python')

  cd voluptuous-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-voluptuous() {
  depends=('python2')

  cd voluptuous-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
