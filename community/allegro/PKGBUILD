# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on pandoc
#  - comment fixing man path

pkgname=allegro
pkgver=5.2.4
pkgrel=2
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64')
url='http://liballeg.org/'
license=('custom')
makedepends=('cmake' 'git' 'glu' 'libtheora' 'mesa-libgl' 'ninja' 'opusfile')
depends=('dumb' 'gtk2' 'jack' 'libgl' 'libpulse' 'libtheora' 'libwebp' 'libxpm'
         'libxxf86dga' 'opusfile' 'physfs')
source=("git+https://github.com/liballeg/${pkgname}${pkgver%%.*}#tag=$pkgver.0")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build

  cmake ../"${pkgname}${pkgver%%.*}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS_HTML=OFF \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 "${pkgname}${pkgver%%.*}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Fix man path
  #mv -v -f "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}

# getver: liballeg.org/a5docs/trunk
# vim: ts=2 sw=2 et:
