# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - don't build with musl-gcc

pkgname=busybox
pkgver=1.29.2
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("x86_64")
url="https://www.busybox.net"
license=('GPL')
makedepends=("ncurses")
source=("$url/downloads/$pkgname-$pkgver.tar.bz2"{,.sig}
        "config")
sha256sums=('67d2fa6e147a45875fe972de62d907ef866fe784c495c363bf34756c444a5d61'
            'SKIP'
            '858835074fd5bb22551f8aceb92581a1132650f75dfb18c0afdbd521a5a9dcfa')
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B') # Denis Vlasenko <vda.linux@googlemail.com>

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir"/config .config
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0755 busybox "$pkgdir"/usr/bin/busybox

  # docs
  install -Dm644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  for doc in BusyBox.html BusyBox.txt; do
    install -Dm644 docs/$doc "$pkgdir"/usr/share/doc/$pkgname/$doc
  done
}
