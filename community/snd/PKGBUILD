# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

pkgname=snd
pkgver=18.4
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('pulseaudio' 'fftw' 'gsl' 'gtk3' 'libmpc' 'ruby')
makedepends=('flac' 'gmp' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('eb7cad432796cb6a846db27e85699791dfc96ea78a5e1d9177cf06942350c04675b56efa564480bf9d99c4d89ccb936ec6bf6401c9751b21b02d3a8f15443fd2')

build() {
  cd "${pkgname}-${pkgver}"
#  LDFLAGS+=" -Wl,-z,noexecstack"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -vDm 644 HISTORY.Snd  "${pkgdir}/usr/share/doc/${pkgname}/HISTORY"
  install -vDm 644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -vDm 644 README.Snd  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
