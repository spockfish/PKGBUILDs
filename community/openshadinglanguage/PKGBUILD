# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed intel-tbb from deps
#  - set -DUSE_LLVM_BITCODE=OFF for v5 and v6

pkgname=openshadinglanguage
pkgver=1.9.9
pkgrel=5
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
sha512sums=('7f3a16bc654676f8e82bf87a2c33914997f1468772ad27bf284c848e9b02adddaf37cb6ef8bde16c81b9076247bca5463a1a5660023efd67d9ac20969ae99647')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  #patch -Np1 < $srcdir/llvm-static-system-libs.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  [ "${CARCH}" == "arm" -o "${CARCH}" == "armv6h" ] && BITCODE="-DUSE_LLVM_BITCODE=OFF"

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLERTTI=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DOSL_BUILD_CPP11=1 \
    -DSTOP_ON_WARNING=OFF \
    $BITCODE
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install
  mkdir "$pkgdir"/usr/share/OSL
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
