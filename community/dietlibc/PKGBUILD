# $Id: PKGBUILD 92521 2013-06-07 16:21:25Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tino Reichardt <milky-archlinux@mcmilk.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches for hardfloat and ensure our cflags are used

pkgname=dietlibc
pkgver=0.33
pkgrel=5
pkgdesc="a libc optimized for small size"
arch=('x86_64')
url="https://www.fefe.de/dietlibc/"
license=("GPL")
options=('staticlibs')
validpgpkeys=('878CBE5DA9A50595E674183F15396A7933EB059A')
#source=(http://www.kernel.org/pub/linux/libs/dietlibc/$pkgname-$pkgver.tar.bz2)
source=("https://www.fefe.de/dietlibc/dietlibc-$pkgver.tar.bz2"{,.sig}
        '0020-fixes-for-ARMv7.diff'
        '0027-fix-arm-mmap.diff'
        '0036-fix-jmp_buf-size-on-armhf.diff')
sha256sums=('68838893790ddd7a42bc1a06e5435054e1dc1914e4b53d2d6c92f603d3b315f5'
            'SKIP'
            'b50724cdbcccc37ce1cba9534c5e5cddd043422bbfbcf0a08b5d8e7f809ab472'
            'ba32b37f96ff41277e50054c24e3e6457b7493926f75c44aa3ed6d693f5e25d7'
            '8b408b8548946571390d0b8bed3068bcbd203ade0b2f8d94b3a190d4be114109')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # patches for hardfloat
  if [ "$CARCH" == 'armv7h' -o "$CARCH" == 'armv6h' ]; then
    sed -i 's/3-6/3-7/' Makefile
    perl -pi -e 's/\r\n/\n/g' arm/md5asm.S
    patch -p1 -i ../0020-fixes-for-ARMv7.diff
    patch -p1 -i ../0027-fix-arm-mmap.diff
    patch -p1 -i ../0036-fix-jmp_buf-size-on-armhf.diff
  fi
  
  # ensure our cflags are used
  sed -i "s/CFLAGS=-pipe/CFLAGS=$CFLAGS/" Makefile

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/bin"
  ln -s /opt/diet/bin/diet "$pkgdir/usr/bin/diet"
}
