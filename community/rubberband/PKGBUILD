# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix build on v5
#  - makedepend on java 8 instead of 10

pkgname=rubberband
pkgver=1.8.2
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('x86_64')
url="https://www.breakfastquay.com/rubberband/"
license=('GPL2')
depends=('libsamplerate' 'fftw' 'vamp-plugin-sdk')
makedepends=('ladspa' 'java-environment=8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/rubberband/archive/v$pkgver.tar.gz"
        '0001-fix-armv5-build.patch')
sha512sums=('b70e41fb1f68487d67b3935682100138ba898adeec9c8e7f8caa30f7bc94389c847d72a1b0b04f042d16d4b57019cc82c0fe79c8f302c3174e603e9bd0289abf'
            'a67d567afba2dc5439a58a0e099ad574ccb8c2c7544185a818ce5ae60c344c3affdcfbf69dcbc8db37bed955fb7388bd7981b8172e3ec56986032b8be0828ec2')

prepare() {
  cd "$pkgname-$pkgver"
  [[ $CARCH == 'arm' ]] && patch -p1 -i ../0001-fix-armv5-build.patch
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk
  make jni
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # docs
  install -vDm 644 {CHANGELOG,README.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
