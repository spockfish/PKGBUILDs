# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind

pkgname=pipewire
pkgver=0.2.2
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base v4l-utils sbc ffmpeg rtkit)
makedepends=(git meson doxygen graphviz xmltoman)
install=pipewire.install
_commit=327ae5db6ef93724640c4c74664d693d7a530dbe  # tags/0.2.2
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit"
        unhide-providers.diff)
sha256sums=('SKIP'
            '8692ddbfc433bb4a56e3e4594683f9a7047b6cce539c7322dd3b74157a727f99')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # the pipewireprovider is broken: the devices provided have no caps
  # at least don't hide other providers until this is fixed
  patch -Np1 -i ../unhide-providers.diff

  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -D enable_docs=true \
    -D enable_man=true \
    -D enable_gstreamer=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
