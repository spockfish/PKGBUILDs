# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add proposed upstream patches to fix Elan I2C touchpads
#  - patch to not crash Xorg on the Chromebook Plus

pkgname=libinput
pkgver=1.7.2
pkgrel=1
pkgdesc="Input device management and event handling library"
arch=(i686 x86_64)
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=(custom:X11)
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
# currently no doc files to install
makedepends=('doxygen' 'graphviz' 'gtk3')
#checkdepends=('check' 'libunwind')
source=(https://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        0001-udev-Add-name-based-input-device-detection-without-d.patch
        0002-udev-Add-support-for-I2C-Elan-touchpads-without-dmi.patch
        0003-udev-Decrease-high-pressure-value-for-Elantech-touch.patch
        0004-Don-t-pair-touchpad-and-lid-switch.patch)
sha512sums=('cdbd2994e954aac9538fe907c275e6e23e2bed0e9c4c65f19591bdcdbf5074131c72b92e87de87c03f75a991fcdb7f568b491a12f00031c4eba11082ca44d69f'
            'SKIP'
            '3c696a723fd1cc32fd851cb8daea47c629d7245c9190e3086813f81ad54197e2e95fc79df4e6ebb956fed539b362a058d4cfb53f364a1ecaba1cbd5cac7a4cab'
            '14a9081f6ca2741d43142251bf38fbd9af445d671472a137e97ccd3827bf01feb076b95c459bcd7947757d919254913fc8c8496976ee5f6f4a4ed5713f7b6f8c'
            'f75895141ef2f78c7ccecbc4b08437c1c4e27078ed7bc52751d3f0b3693ca2820c85aacb4435d4fe950d43ea940e90896c55423a80698762d20b740a64f12884'
            'db8d684d3370e860764b52cc4f2287630cf58dad337684a7ae485957b8cec54cd2da47e27b5c8eefa42f753d366a4cda3d957cf0514087b3013ac7562fa785f3')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-udev-Add-name-based-input-device-detection-without-d.patch
  patch -p1 -i ../0002-udev-Add-support-for-I2C-Elan-touchpads-without-dmi.patch
  patch -p1 -i ../0003-udev-Decrease-high-pressure-value-for-Elantech-touch.patch
  patch -p1 -i ../0004-Don-t-pair-touchpad-and-lid-switch.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  # install doc - no Makefile target
  install -v -dm755 ${pkgdir}/usr/share/doc/libinput
  cp -rv doc/html/* ${pkgdir}/usr/share/doc/libinput
}
