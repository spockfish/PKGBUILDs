# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch for chromium GN

buildarch=12
highmem=1

pkgname=qt5-webengine
_qtver=5.11.1
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='http://qt-project.org/'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'protobuf' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'git' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtwebengine-harmony.patch qtwebengine-ffmpeg4.patch
         qtwebengine-wayland.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=a66d4cd8"
         qtbug-68699.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=6e019f2a"
         0001-ARM-toolchain-fixes.patch)
sha256sums=('389d9f42ca393ac11ec8932ce9771766dec91a4c761ffb685cc429c2a760d48c'
            'feca54ab09ac0fc9d0626770a6b899a6ac5a12173c7d0c1005bc3964ec83e7b3'
            '4a831d89fb0d6a6ced23115ced71e60513ce279fba4bd493178842647948f510'
            '6baeb241165a1fd94ada95b27924ba55f74761b1b9d406d73ebcce4b139abf60'
            '1f3eff42911e2ad6e6db8b2c5d4b230031c577e3396f37b1e73c33bcef16f6bb'
            '8202b09a1caa82538a2eacd79b62b61d8661c65cdfb275560d231aa31a362b12')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd ${_pkgfqn}

  # FreeType 2.8.1
  patch -Np1 -i ../qtwebengine-harmony.patch
  # Fix openGL detection on Wayland
  patch -p1 -i ../qtwebengine-wayland.patch
  # Fix crash when scrolling on touchscreen https://bugreports.qt.io/browse/QTBUG-68699
  patch -p1 -i ../qtbug-68699.patch

  cd src/3rdparty/chromium
  # Fix build with ffmpeg 4
  patch -p2 -i "$srcdir"/qtwebengine-ffmpeg4.patch

#  cd src/3rdparty
  cd ..
  patch -p1 -i ${srcdir}/0001-ARM-toolchain-fixes.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
