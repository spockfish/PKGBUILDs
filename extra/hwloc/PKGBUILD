# $Id$
# Maintainer : Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>
# Contributor: Hervé YVIQUEL <elldekaa@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove numactl dependency

pkgname=hwloc
pkgver=1.11.7
pkgrel=1
pkgdesc='Portable Hardware Locality is a portable abstraction of hierarchical architectures'
arch=('i686' 'x86_64')
url='http://www.open-mpi.org/projects/hwloc/'
license=('BSD')
depends=('libtool')
optdepends=('cairo' 'libxml2' 'pciutils')
makedepends=('cairo' 'libxml2' 'pciutils')
options=('!docs')
source=(http://www.open-mpi.org/software/hwloc/v1.11/downloads/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('7b78850b8504a93972a3f114a0e75f9b115607d9')

build() {
  cd hwloc-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-plugins
  make
}

check() {
  cd hwloc-$pkgver
  make check
}

package() {
  cd hwloc-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/hwloc/LICENSE
}
