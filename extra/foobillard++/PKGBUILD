# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable fast math

pkgname=foobillard++
pkgver=3.42beta
pkgrel=6
pkgdesc="An OpenGL billiard game"
arch=('x86_64')
url="http://foobillardplus.sourceforge.net/"
license=('GPL2')
depends=('freetype2' 'libpng' 'glu' 'sdl_net' 'sdl_mixer')
replaces=('foobillard')
source=(http://downloads.sourceforge.net/foobillardplus/foobillardplus-${pkgver}.tar.gz)
sha1sums=('51507efab0f595cfff26149003faf669c0a42f32')

prepare() {
  cd foobillardplus-${pkgver}
  sed -i 's|/opt/foobillardplus/bin/||' foobillardplus.desktop
  sed -i 's|/opt/foobillardplus/||' foobillardplus.desktop
  sed -e 's|freetype-config|pkg-config freetype2|g' -i src/Makefile.am
  sed -e 's|inline float|float|g' -i src/vmath.*
}

build() {
  cd foobillardplus-${pkgver}
  aclocal --force
  autoconf -f
  autoheader -f
  automake -a -c -f 
  ./configure --prefix=/usr/share --disable-fastmath
  make
}

package() {
  cd foobillardplus-${pkgver}
  make DESTDIR="${pkgdir}" datadir="${pkgdir}/usr/share/foobillardplus/data" \
    prefix="$pkgdir"/usr/share install

  install -d "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  mv "${pkgdir}"/usr/share/{AUTHORS,COPYING,ChangeLog,INSTALL,README,TODO} "${pkgdir}/usr/share/foobillardplus" 
  mv "${pkgdir}"/usr/share/foobillardplus.desktop "${pkgdir}"/usr/share/applications
  mv "${pkgdir}"/usr/share/foobillardplus.{png,xbm} "${pkgdir}"/usr/share/pixmaps
  ln -s /usr/share/foobillardplus/bin/foobillardplus "${pkgdir}"/usr/bin/foobillardplus
}
