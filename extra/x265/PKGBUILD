# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz@interia.pl>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to attempt fixing ARM hackery by bandwagoning developers
#  - retain 10/12 bit separation for AArch64
#  - upstream patch to fix ARM FTBFS
#  - disable all ARM assembly - version 2.8 broke ARM support completely

pkgname=x265
pkgver=2.8
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('x86_64')
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
depends=('gcc-libs')
makedepends=('cmake' 'nasm')
provides=('libx265.so')
source=("https://bitbucket.org/multicoreware/x265/downloads/x265_${pkgver}.tar.gz"
        arm.patch
        https://github.com/videolan/x265/commit/8911cbae1f7f868b213c5c0aa41cf827cdf367bb.patch)
sha256sums=('6e59f9afc0c2b87a46f98e33b5159d56ffb3558a49d8e3d79cb7fdc6b7aaa863'
            'fdd9113a015679652224f2d62c4bbbffe9a15bfef8c6a928aaf2fc57d1cd8fd8'
            'edd298ac1be919d325e618a20231a0f84c783fbdcba30e0e2372f69f471bd1cd')

prepare() {
  cd x265_${pkgver}

  patch -p1 -i ../arm.patch
  patch -p1 -i ../8911cbae1f7f868b213c5c0aa41cf827cdf367bb.patch
  [[ $CARCH == "aarch64" ]] && CFLAGS+=" -fPIC" && CXXFLAGS+=" -fPIC"
  [[ $CARCH != "aarch64" ]] && CONFIG="-DENABLE_ASSEMBLY=OFF"

  for d in 8 $([[ $CARCH == 'aarch64' ]] && echo "10 12"); do
    if [[ -d build-$d ]]; then
      rm -rf build-$d
    fi
    mkdir build-$d
  done
}

build() {
if [[ $CARCH == aarch64 ]]; then

  cd x265_${pkgver}/build-12

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHIGH_BIT_DEPTH='TRUE' \
    -DMAIN12='TRUE' \
    -DEXPORT_C_API='FALSE' \
    -DENABLE_CLI='FALSE' \
    -DENABLE_SHARED='FALSE'
  make

  cd ../build-10

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DHIGH_BIT_DEPTH='TRUE' \
    -DEXPORT_C_API='FALSE' \
    -DENABLE_CLI='FALSE' \
    -DENABLE_SHARED='FALSE'
  make

  cd ../build-8

  ln -s ../build-10/libx265.a libx265_main10.a
  ln -s ../build-12/libx265.a libx265_main12.a

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_SHARED='TRUE' \
    -DENABLE_HDR10_PLUS='TRUE' \
    -DEXTRA_LIB='x265_main10.a;x265_main12.a' \
    -DEXTRA_LINK_FLAGS='-L .' \
    -DLINKED_10BIT='TRUE' \
    -DLINKED_12BIT='TRUE'
  make

else

  cd x265_${pkgver}/build-8

  cmake ../source \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_SHARED='TRUE' \
    $CONFIG

fi
}

package() {
  cd x265_${pkgver}/build-8

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
