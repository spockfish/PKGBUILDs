# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to replace deprecated SSL_ST_OK state with TLS_ST_OK (1)

pkgname=perl-anyevent
pkgver=7.13
pkgrel=1.1
epoch=4
pkgdesc='The DBI of event loop programming'
arch=('any')
url="https://metacpan.org/release/AnyEvent"
license=('PerlArtistic' 'GPL')
depends=('perl')
optdepends=('perl-async-interrupt: for fast signal handling'
            'perl-ev: for the recommended EV event loop backend'
            'perl-guard: to speed up guards'
            'perl-json: send or receive JSON with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle'
            'perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-$pkgver.tar.gz"
        'openssl.patch')
md5sums=('70eea152cf4686f8f351aa8bc96157f9'
         '45b5b96cfd67a1ae2c1c36d4d7941c0d')

prepare() {
  cd AnyEvent-$pkgver
  patch -p1 -i ../openssl.patch
}

build() {
  cd AnyEvent-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd AnyEvent-$pkgver
  make test
}

package() {
  cd AnyEvent-$pkgver
  make DESTDIR="$pkgdir" install
}
