# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to replace deprecated SSL_ST_OK state with TLS_ST_OK (1)

pkgname=perl-anyevent
pkgver=7.13
pkgrel=2
epoch=4
pkgdesc='The DBI of event loop programming'
arch=('any')
url="https://metacpan.org/release/AnyEvent"
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('glib-perl' 'perl-async-interrupt' 'perl-ev' 'perl-net-ssleay')
optdepends=('perl-async-interrupt: for fast signal handling'
            'perl-ev: for the recommended EV event loop backend'
            'perl-guard: to speed up guards'
            'perl-json: send or receive JSON with AnyEvent::Handle'
            'perl-json-xs: send or receive JSON with AnyEvent::Handle'
            'perl-net-ssleay: TLS/SSL connections with AnyEvent::Handle')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/AnyEvent-$pkgver.tar.gz"
        issue53923.patch
        openssl.patch)
md5sums=('70eea152cf4686f8f351aa8bc96157f9'
         'e6e9e44caa1610a1d527c76258270d7a'
         'fa31c399ba591a634c3165420a273f33')

prepare() {
  cd AnyEvent-$pkgver
  # https://rt.cpan.org/Public/Bug/Display.html?id=118584
  patch -p1 -i ../issue53923.patch
  patch -p1 -i ../openssl.patch
}

build() {
  cd AnyEvent-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd AnyEvent-$pkgver
  PERL_ANYEVENT_LOOP_TESTS=true make test
}

package() {
  cd AnyEvent-$pkgver
  make DESTDIR="$pkgdir" install
}
