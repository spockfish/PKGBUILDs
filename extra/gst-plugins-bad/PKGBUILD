# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - drop valgrind makedepend
#  - remove --enable-gtk-doc from configure - broken with latest gtk-doc

pkgname=gst-plugins-bad
pkgver=1.12.2
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(mjpegtools gst-plugins-base-libs curl chromaprint libmms faad2 celt libdca libdvdnav
         libmodplug libgme wayland libofa openjpeg2 libwebp libsrtp gnutls glu sbc rtmpdump
         libgudev graphene schroedinger libexif libdvdread libvdpau libmpeg2 wildmidi ladspa
         openal libusb vulkan-icd-loader libfdk-aac faac soundtouch spandsp neon
         webrtc-audio-processing libdc1394 libmpcdec zvbi)
makedepends=(python gobject-introspection gtk-doc git autoconf-archive vulkan-headers
             gtk3 clutter librsvg libtiger qt5-declarative qt5-x11extras qt5-wayland zbar
             fluidsynth lilv opencv openexr)
_commit=0a4f4f7c2d0185c91ac3c768a8e8d20dc292f8ee  # tags/1.12.2^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-bad#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common"
        'https://github.com/GStreamer/gst-plugins-bad/commit/15f24fef53a955c7c76fc966302cb0453732e657.patch' # OpenJPEG 2.2 support
        'https://github.com/GStreamer/gst-plugins-bad/commit/df4c6385aa0739d59e83916d32b704de9b9f3d40.patch') # OpenJPEG 2.2 support
sha256sums=('SKIP'
            'SKIP'
            'c1b18a2d7008b4a9a50f4bbf9e758ccc1777cf0d4399826f059de4685f587fc0'
            '5c2dbc5738c3466e523ec2872edc39ef6ed1f4217eda149df984e40202674ab9')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  patch -Np1 -i ../15f24fef53a955c7c76fc966302cb0453732e657.patch
  patch -Np1 -i ../df4c6385aa0739d59e83916d32b704de9b9f3d40.patch

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  sed -i 's/cmu_us_kal/&16/g' configure.ac ext/flite/gstflitetestsrc.c

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer Bad Plugins (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --with-gtk=3.0 \
    --enable-experimental --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  # bad tests are bad
  #make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
