# $Id: PKGBUILD 87501 2013-04-02 00:06:04Z dreisner $
# Maintainer: Dave Reisner <d@falconindy.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream commit to fix stdin on ARM

pkgname=expac
pkgver=9
pkgrel=1.1
pkgdesc="pacman database extraction utility"
arch=('x86_64')
url="http://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('perl')
source=("https://pkgbuild.com/~dreisner/sources/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        'https://github.com/falconindy/expac/commit/8d6429b374d8291d2ac544186046d6b5c88cdc18.patch')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
md5sums=('d1250004182effe1769cfd269d5df2fb'
         'SKIP'
         'ee9669ee119cf099cdb8845708d2253d')

prepare() {
  cd "$pkgname-$pkgver"

  sed '/\*\//q' expac.c >LICENSE
  patch -p1 -i ../8d6429b374d8291d2ac544186046d6b5c88cdc18.patch
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
