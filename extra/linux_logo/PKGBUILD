# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Fedora for AArch64 support

pkgname=linux_logo
pkgver=5.11
pkgrel=4
pkgdesc="Text-based logo and system information program"
arch=('x86_64')
url="http://www.deater.net/weave/vmwprod/linux_logo"
license=('GPL')
depends=('glibc')
source=(http://www.deater.net/weave/vmwprod/linux_logo/${pkgname}-${pkgver}.tar.gz
        0001-initial-aarch64-support.patch)
sha512sums=('3c11d59eeb1ea613eb66d3ea5ef2d7c9ef906cb12430b9350570a6a1937ec174bdd974e6227358339c3fd2e0647a5066a0bea22289c4aa9bc8b03afb0033f114'
            '09096864e9e06210cab637cd00e42451ccd21602d41f7e2ee22501409cae2c437dccd1158680887fd48e3cfb3172b48792be435d9dc75c56fdd203453e968663')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-initial-aarch64-support.patch
}

build() {
  cd ${pkgname}-${pkgver}
  find ./logos -type f | sort > logo_config
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
}
