# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Alpine Linux to fix cache detection issues on ARM

pkgname=haveged
pkgver=1.9.1
pkgrel=4.1
pkgdesc='Entropy harvesting daemon using CPU timings'
arch=('i686' 'x86_64')
url='http://www.issihosts.com/haveged'
license=('GPL')
depends=('glibc')
validpgpkeys=('4CFBA9F06A360837CE23919DA24EDC651E265688') # Gary Wuertz <gary@issiweb.com>
source=("$url/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/verify/$pkgname-$pkgver.sig"
        'haveged.service'
        'fix-cpu-cache-size-detection.patch')
sha256sums=('9c2363ed9542a6784ff08e247182137e71f2ddb79e8e6c1ac4ad50d21ced3715'
            'SKIP'
            '43540fbff57c5106e4283dd7b331499648e38ba15e84898ef5e0a53cccc0200b'
            '5aecfd72a1dbb9cc9b32d5677917d8a59dba7a2e021e31dee95f3f5b9e7ecf28')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-cpu-cache-size-detection.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
      --prefix=/usr \
      --bindir=/usr/bin \
      --sbindir=/usr/bin
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -Dm644 haveged.service "$pkgdir"/usr/lib/systemd/system/haveged.service
}
