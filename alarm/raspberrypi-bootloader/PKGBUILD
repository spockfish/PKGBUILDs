# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader'
         'raspberrypi-bootloader-x')
pkgver=20170728
pkgrel=1
_commit=23047785b7414111cb7cb80aa9d0042c99fae437
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('c5c018a3f9c874d4e5b55c8fccc1661c')

package_raspberrypi-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}
