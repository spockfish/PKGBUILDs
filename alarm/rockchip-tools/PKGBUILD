# Rockchip Tools
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=12

pkgname=rockchip-tools
pkgver=20170420
pkgrel=1
pkgdesc="Rockchip Tools"
arch=('armv7h' 'aarch64')
url="https://github.com/neo-technologies"
depends=('openssl' 'libusb')
makedepends=('git')
license=('custom' 'APACHE' 'GPL')
source=("git+https://github.com/neo-technologies/rkflashtool.git"
        "git+https://github.com/neo-technologies/rockchip-mkbootimg.git"
        "git+https://github.com/neo-technologies/rkboottools.git"
        "git+https://github.com/rockchip-linux/rkdeveloptool.git")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  cd "${srcdir}/rkflashtool"
  make

  cd "${srcdir}/rockchip-mkbootimg"
  sed -i 's/LDFLAGS ?=/LDFLAGS +=/' Makefile
  make

  cd "${srcdir}/rkboottools"
  make

  cd "${srcdir}/rkdeveloptool"
  CXXFLAGS+=" -fsigned-char"
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  install -d "${pkgdir}"/usr/bin

  cd "${srcdir}/rkflashtool"
  install -m755 rkcrc rkflashtool rkmisc rkpad rkparameters rkparametersblock rkunpack rkunsign "${pkgdir}"/usr/bin

  cd "${srcdir}/rockchip-mkbootimg"
  install -m755 afptool img_maker mkbootimg mkcpiogz mkrootfs mkupdate unmkbootimg unmkcpiogz "${pkgdir}"/usr/bin

  cd "${srcdir}/rkboottools"
  install -m755 rk-makebootable rk-rc4 rk-splitboot "${pkgdir}"/usr/bin

  cd "${srcdir}/rkdeveloptool"
  make DESTDIR="${pkgdir}" install
}
