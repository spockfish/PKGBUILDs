# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jonny Gerold <jonny@fsk141.com>
# Contributor: Julian Langschaedel <meta.rb@gmail.com>

pkgname=uboot-tools
pkgver=2017.03
pkgrel=2
pkgdesc="U-Boot Tools"
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
license=('GPL' 'LGPL')
provides=('uboot-env' 'uboot-mkimage')
replaces=('uboot-env' 'uboot-mkimage')
backup=(etc/fw_env.config)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2
        0001-rsa-Fix-build-with-OpenSSL-1.1.x.patch
        0002-rsa-Fix-deprecated-warnings-for-OpenSSL-1.1.x.patch
        0003-tools-kwbimage-fix-build-with-OpenSSL-1.1.x.patch
        fw_env.config)
md5sums=('52fed6ce16e0c4a50e2cd4defdf9097a'
         '0b9dbef637a2d063f4282cc23f1f8fbc'
         'b21e23f823fcbdabbb84da568e9ac10c'
         'ba73fb9d3da714745729d6114f91538c'
         '5a9dcbae5957f8621080d4bbc0e1520f')

prepare() {
  cd u-boot-${pkgver}

  patch -p1 -i ../0001-rsa-Fix-build-with-OpenSSL-1.1.x.patch
  patch -p1 -i ../0002-rsa-Fix-deprecated-warnings-for-OpenSSL-1.1.x.patch
  patch -p1 -i ../0003-tools-kwbimage-fix-build-with-OpenSSL-1.1.x.patch
}

build() {
  cd u-boot-${pkgver}

  make defconfig
  make tools-all CONFIG_KIRKWOOD=y
}

package() {
  cd u-boot-${pkgver}

  install -dm755 ${pkgdir}/{usr/{bin,share/man/man1},etc}
  install -m755 tools/{fit_{check_sign,info},dumpimage,mkimage,mkenvimage,netconsole,ncb,proftool,env/fw_printenv,kwboot} ${pkgdir}/usr/bin
  ln -s /usr/bin/fw_printenv ${pkgdir}/usr/bin/fw_setenv
  install -m644 ${srcdir}/fw_env.config ${pkgdir}/etc
  install -m644 doc/{mkimage,kwboot}.1 ${pkgdir}/usr/share/man/man1
}
