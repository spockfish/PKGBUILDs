# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20180531
pkgrel=3
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='https://git.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"{,.sig}
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch')
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'
              '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC'
              '86CFFCA918CF3AF47147588051E8B148A9999C34'
              '8FC15A064950A99DD1BD14DD39E4B877E62EB915'
              '8218F88849AAC522E94CF470A5E9288C4FA415FA'
              'B81B051F2D7FC867AAFF35A58DBD63B82072D77A'
              'F3691687D867B81B51CE07D9BBE43771487328A9'
              '6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD')
md5sums=('ed3c757184e633faeea09747a7b91bb1'
         'SKIP'
         '5233b5e7572c58810e3392ca03aa2394'
         '5bebd338b48d76b87c46175e034e3518'
         'bfba8bb3677ec381e2356ff7e4a70f24'
         '2a46eb18fc4bb549099e5eda321a8f0d'
         '154a0953d8f2fc6e226ad0302e9f318f'
         '91b9152cfa71911d55e0fff0b18c7e5b'
         'd92b4828159222c8aef0940b4cf13eaa')

prepare() {
	cd "${_pkgname}-${pkgver}"
	patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
	patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i ../0003-makechrootpkg-distcc.patch
	patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
	patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i ../0006-archbuild-no-setarch.patch
	patch -p1 -i ../0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch
}

build() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
}
