# ArmSoc (OMAP + Mali)  FrameBuffer driver for Xorg from Linaro.
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Erik Andersson <erik.h.andersson@gmail.com>

buildarch=4

pkgname=xf86-video-armsoc-git
pkgver=258.8bbdb2a
pkgrel=4
epoch=1
pkgdesc='X.org graphics driver for ARM graphics'
arch=('armv7h')
url="http://cgit.freedesktop.org/xorg/driver/xf86-video-armsoc"
license=('GPL2')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=24.0' 'git')
conflicts=('xorg-server<1.20' 'X-ABI-VIDEODRV_VERSION<24' 'X-ABI-VIDEODRV_VERSION>=25' 'xf86-video-armsoc')
provides=(xf86-video-armsoc)
source=("$pkgname::git://anongit.freedesktop.org/xorg/driver/xf86-video-armsoc"
        '0001-Initialize-XRandR-before-EnterVT.patch')
md5sums=('SKIP'
         '07cf643ee2dad206d0a33c14db985a06')

pkgver() {
  cd $pkgname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  git apply ../0001-Initialize-XRandR-before-EnterVT.patch
}

build() {
  cd $pkgname

  # Since pacman 5.0.2-2, hardened flags are now enabled in makepkg.conf
  # With them, module fail to load with undefined symbol.
  # See https://bugs.archlinux.org/task/55102 / https://bugs.archlinux.org/task/54845
  export CFLAGS=${CFLAGS/-fno-plt}
  export CXXFLAGS=${CXXFLAGS/-fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  CFLAGS=`echo $CFLAGS | sed -e 's/vfpv3-d16/neon/'` && CXXFLAGS="$CFLAGS"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname

  make DESTDIR="${pkgdir}" install
}

